#!/usr/bin/expect
set timeout 30
set pathToAudio [lindex $argv 0]
set expectedRecoText [lindex $argv 1]
set noWaitForExit [lindex $argv 2]
set cmd [lrange $argv 3 end]
spawn {*}$cmd
set exitCode 1
expect {
  "Say something...\r\n" {
    puts "### Running paplay [set pathToAudio]"
    exec sleep 1
    exec paplay [set pathToAudio]

    expect {
      -re "(We r|R)ecognized: (.+?)\r\n" {
        set recoText $expect_out(2,string)
        if {$recoText == $expectedRecoText} {
          puts "### Pass with the right recognition text"
          set exitCode 0
        } else {
          puts "### BAD RECO TEXT: expected != actual: [set expectedRecoText] != [set recoText]"
        }
      }
      timeout {
        puts "### TIMEOUT waiting for result"
      }
    }
  }
  timeout {
    puts "### TIMEOUT waiting for prompt"
  }
}
if {$noWaitForExit == 1} {
  puts "### Exiting right away"
  exit [set exitCode]
}
expect {
  "Please press a key to continue." {
    send "a\r"
  }
  timeout {
    puts "### TIMEOUT waiting for end-of-program prompt"
  }
}
expect {
  eof { }
  timeout {
    puts "### TIMEOUT waiting for program exit"
  }
}
exit [set exitCode]
