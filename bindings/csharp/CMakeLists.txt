project(carbon_csharp)

find_package(SWIG REQUIRED)
include(UseSWIG)

set(SRC_DIR "${PROJECT_SOURCE_DIR}")
set(INC_DIR "${PROJECT_SOURCE_DIR}/include")

# Build the cxx binding dll via swig

set(SOURCES "${SRC_DIR}/carbon_csharp.i")

if(MSVC)
     # Force to always compile with W4
     set(CMAKE_CXX_FLAGS "/W3")
elseif(CMAKE_COMPILER_IS_GNUCC OR CMAKE_COMPILER_IS_GNUCXX)
     set(CMAKE_CXX_FLAGS "-std=c++11 -Wall -Wextra -Wno-unused-parameter -fPIC")
endif()

set(CSHARP_BINDINGS ${PROJECT_NAME}_bindings)

set(SWIG_MODULE_${CSHARP_BINDINGS}_EXTRA_DEPS
    ${SWIG_INTERFACE}
    "${SRC_DIR}/carbon_csharp.i"
    ${SPEECH_C_API_HEADERS}
    ${SPEECH_CXX_API_HEADERS})

# ${SWIG_INTERFACE} should resolve to {repo/root}/bindings/carbon.i
set(CMAKE_SWIG_FLAGS -namespace Carbon)
set_source_files_properties("${SRC_DIR}/carbon_csharp.i" PROPERTIES
    CPLUSPLUS ON)

# Make sure the nested directory structure exists
set(CSHARP_SOURCE_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/swig-generated CACHE INTERNAL "")
file(REMOVE_RECURSE ${CSHARP_SOURCE_DIRECTORY})
file(MAKE_DIRECTORY ${CSHARP_SOURCE_DIRECTORY})
# Create swig target
set(CMAKE_SWIG_OUTDIR ${CSHARP_SOURCE_DIRECTORY})

include_directories(${INC_DIR})
include_directories(${CARBON_C_API})
include_directories(${CARBON_CXX_API})
include_directories("${SRC_DIR}/..")

swig_add_library(${CSHARP_BINDINGS} LANGUAGE csharp SOURCES ${SOURCES})
swig_link_libraries(${CSHARP_BINDINGS} carbon)
set_target_properties(${SWIG_MODULE_${CSHARP_BINDINGS}_REAL_NAME} PROPERTIES LIBRARY_OUTPUT_DIRECTORY ${CMAKE_RUNTIME_OUTPUT_DIRECTORY})
set_target_properties(${SWIG_MODULE_${CSHARP_BINDINGS}_REAL_NAME} PROPERTIES
    FOLDER bindings/csharp
)

# Build the C# wrapper library from the cs files generated by swig.
set(SWIG_CS_SOURCES ${CSHARP_SOURCE_DIRECTORY}/*.cs)
SET_SOURCE_FILES_PROPERTIES(${SWIG_CS_SOURCES} PROPERTIES GENERATED TRUE)

set(CSHARP_LIB ${PROJECT_NAME}_lib CACHE INTERNAL "")

enable_language(CSharp)
add_library(${CSHARP_LIB} SHARED ${SWIG_CS_SOURCES})
add_dependencies(${CSHARP_LIB} ${CSHARP_BINDINGS} ${SWIG_MODULE_${CSHARP_BINDINGS}_REAL_NAME})
set_target_properties(${CSHARP_LIB} PROPERTIES
    FOLDER bindings/csharp
)

set_property(DIRECTORY ${CMAKE_SOURCE_DIR} APPEND PROPERTY CMAKE_CONFIGURE_DEPENDS ${SWIG_INTERFACE})