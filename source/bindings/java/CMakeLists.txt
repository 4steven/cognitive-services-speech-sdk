project(com.microsoft.cognitiveservices.speech)

find_package(SWIG REQUIRED)
find_package(Java REQUIRED)

if(WIN32 OR UNIX AND NOT ANDROID)
    find_package(JNI REQUIRED)
endif()

include(UseJava)
include(UseSWIG)

#
# Set up the Swig target, ${JAVA_BINDINGS}
#

set(JAVA_BINDINGS Microsoft.CognitiveServices.Speech.java.bindings)

set(SWIG_JAVA_INTERFACE "${PROJECT_SOURCE_DIR}/carbon_java.i")

set(SWIG_PACKAGE "${PROJECT_NAME}.internal")

string(REPLACE . / SWIG_PACKAGE_DIR ${SWIG_PACKAGE})

set(CMAKE_SWIG_OUTDIR ${CMAKE_CURRENT_BINARY_DIR}/swig-generated/${SWIG_PACKAGE_DIR})

file(REMOVE_RECURSE ${CMAKE_SWIG_OUTDIR})
file(MAKE_DIRECTORY ${CMAKE_SWIG_OUTDIR})

if(CMAKE_COMPILER_IS_GNUCC OR CMAKE_COMPILER_IS_GNUCXX)
    set(CMAKE_CXX_FLAGS "-Wno-unused-function -fno-strict-aliasing")
elseif(ANDROID)
    # Since we use CLANG, we need to set __GNUC__=4 because otherwise the generated Swig bindings won't export the symbols
    set(CMAKE_CXX_FLAGS "-D__GNUC__=4 -std=c++11 -Wall -Wextra -Wno-unused-function -Wno-unused-parameter -fno-strict-aliasing -fPIC")
endif()

if(WIN32)
  set(SPEECHSDK_ORIGINAL_FILENAME "${JAVA_BINDINGS}.dll")
  configure_file(
    ${CARBON_ROOT}/ci/version.rc.in
    ${CMAKE_CURRENT_BINARY_DIR}/windows/version.rc)

  set(SOURCES_VERSION "${CMAKE_CURRENT_BINARY_DIR}/windows/version.rc")
else()
  set(SOURCES_VERSION "")
endif()

# ${SWIG_INTERFACE} and ${SWIG_WRAPPERS} point to common interface / header files
set(SWIG_MODULE_${JAVA_BINDINGS}_EXTRA_DEPS
    ${SWIG_INTERFACE}
    ${SWIG_WRAPPERS}
    ${SPEECH_C_API_HEADERS}
    ${SPEECH_CXX_API_HEADERS})

# warning 401: Nothing known about base class 'X'. Ignored.
# warning 503: Can't wrap 'X' unless renamed to a valid identifier.
set(CMAKE_SWIG_FLAGS -w401,503 -package ${SWIG_PACKAGE})
set_source_files_properties(${SWIG_JAVA_INTERFACE} PROPERTIES CPLUSPLUS ON)

include_directories(${CARBON_C_API})
include_directories(${CARBON_CXX_API})
include_directories(${JAVA_INCLUDE_PATH})
include_directories(${JNI_INCLUDE_DIRS})
include_directories("${PROJECT_SOURCE_DIR}/..")

if("${CMAKE_VERSION}" VERSION_GREATER 3.8)
    swig_add_library(${JAVA_BINDINGS} LANGUAGE java SOURCES ${SWIG_JAVA_INTERFACE} ${SOURCES_VERSION})
else()
    swig_add_module(${JAVA_BINDINGS} java ${SWIG_JAVA_INTERFACE} ${SOURCES_VERSION})
endif()

if(WIN32 AND SPECTRE_MITIGATION)
    set_property(TARGET ${JAVA_BINDINGS} PROPERTY VS_GLOBAL_SpectreMitigation "Spectre")
endif()

swig_link_libraries(${JAVA_BINDINGS} ${SPEECHSDK_CORE_LIBRARY})

if(WIN32)
    # Ensure we build with debug information (TODO global setting does not apply?)
    set_target_properties(${JAVA_BINDINGS} PROPERTIES LINK_FLAGS "/DEBUG /OPT:REF /OPT:ICF")
elseif(APPLE)
    set_target_properties(${JAVA_BINDINGS}
      PROPERTIES BUILD_RPATH "@loader_path")
elseif(UNIX)
    set_target_properties(${JAVA_BINDINGS} PROPERTIES BUILD_RPATH "$ORIGIN")
endif()

set_target_properties(${SWIG_MODULE_${JAVA_BINDINGS}_REAL_NAME}
  PROPERTIES
  LIBRARY_OUTPUT_DIRECTORY ${CMAKE_RUNTIME_OUTPUT_DIRECTORY}
  FOLDER bindings/java)

# Define list of Java sources generated by Swig
# TODO this should be a wildcard, not an explicit list
set(SWIG_JAVA_SOURCES
    ${CMAKE_SWIG_OUTDIR}/AudioConfig.java
    ${CMAKE_SWIG_OUTDIR}/AudioInputStream.java
    ${CMAKE_SWIG_OUTDIR}/AudioOutputStream.java
    ${CMAKE_SWIG_OUTDIR}/AudioStreamFormat.java
    ${CMAKE_SWIG_OUTDIR}/PushAudioInputStream.java
    ${CMAKE_SWIG_OUTDIR}/PullAudioInputStream.java
    ${CMAKE_SWIG_OUTDIR}/PullAudioInputStreamCallback.java
    ${CMAKE_SWIG_OUTDIR}/PullAudioOutputStream.java
    ${CMAKE_SWIG_OUTDIR}/PushAudioOutputStream.java
    ${CMAKE_SWIG_OUTDIR}/PushAudioOutputStreamCallback.java

    ${CMAKE_SWIG_OUTDIR}/BaseAsyncRecognizer.java
    ${CMAKE_SWIG_OUTDIR}/BaseRecognizerBase.java
    ${CMAKE_SWIG_OUTDIR}/PropertyId.java
    ${CMAKE_SWIG_OUTDIR}/carbon_java.java
    ${CMAKE_SWIG_OUTDIR}/carbon_javaJNI.java
    ${CMAKE_SWIG_OUTDIR}/carbon_javaConstants.java
    ${CMAKE_SWIG_OUTDIR}/EventArgs.java
    ${CMAKE_SWIG_OUTDIR}/IntentEventListener.java
    ${CMAKE_SWIG_OUTDIR}/IntentEventSignal.java
    ${CMAKE_SWIG_OUTDIR}/IntentCanceledEventSignal.java
    ${CMAKE_SWIG_OUTDIR}/IntentRecognitionCanceledEventArgs.java
    ${CMAKE_SWIG_OUTDIR}/IntentCanceledEventListener.java
    ${CMAKE_SWIG_OUTDIR}/IntentRecognitionEventArgs.java
    ${CMAKE_SWIG_OUTDIR}/IntentRecognitionResult.java
    ${CMAKE_SWIG_OUTDIR}/IntentRecognitionResultPtrFuture.java
    ${CMAKE_SWIG_OUTDIR}/IntentRecognizer.java
    ${CMAKE_SWIG_OUTDIR}/IntentRecognizerBase.java
    ${CMAKE_SWIG_OUTDIR}/IntentTrigger.java 
    ${CMAKE_SWIG_OUTDIR}/KeywordRecognitionModel.java
    ${CMAKE_SWIG_OUTDIR}/LanguageUnderstandingModel.java
    ${CMAKE_SWIG_OUTDIR}/OutputFormat.java
    ${CMAKE_SWIG_OUTDIR}/AudioStreamContainerFormat.java
    ${CMAKE_SWIG_OUTDIR}/PropertyCollection.java

    ${CMAKE_SWIG_OUTDIR}/ResultReason.java
    ${CMAKE_SWIG_OUTDIR}/CancellationReason.java
    ${CMAKE_SWIG_OUTDIR}/CancellationDetails.java
    ${CMAKE_SWIG_OUTDIR}/CancellationErrorCode.java    
    ${CMAKE_SWIG_OUTDIR}/NoMatchReason.java
    ${CMAKE_SWIG_OUTDIR}/NoMatchDetails.java
    ${CMAKE_SWIG_OUTDIR}/RecognitionEventArgs.java
    ${CMAKE_SWIG_OUTDIR}/RecognitionEventListener.java
    ${CMAKE_SWIG_OUTDIR}/RecognitionEventSignal.java
    ${CMAKE_SWIG_OUTDIR}/RecognitionResult.java
    ${CMAKE_SWIG_OUTDIR}/Recognizer.java

    ${CMAKE_SWIG_OUTDIR}/Session.java
    ${CMAKE_SWIG_OUTDIR}/SessionEventArgs.java
    ${CMAKE_SWIG_OUTDIR}/SessionEventListener.java
    ${CMAKE_SWIG_OUTDIR}/SessionEventSignal.java
    ${CMAKE_SWIG_OUTDIR}/Connection.java
    ${CMAKE_SWIG_OUTDIR}/Grammar.java
    ${CMAKE_SWIG_OUTDIR}/PhraseListGrammar.java
    ${CMAKE_SWIG_OUTDIR}/ConnectionEventArgs.java
    ${CMAKE_SWIG_OUTDIR}/ConnectionEventListener.java
    ${CMAKE_SWIG_OUTDIR}/ConnectionEventSignal.java
    ${CMAKE_SWIG_OUTDIR}/SpeechConfig.java
    ${CMAKE_SWIG_OUTDIR}/SpeechRecognitionCanceledEventArgs.java
    ${CMAKE_SWIG_OUTDIR}/SpeechRecognitionCanceledEventListener.java
    ${CMAKE_SWIG_OUTDIR}/SpeechRecognitionEventArgs.java
    ${CMAKE_SWIG_OUTDIR}/SpeechRecognitionEventListener.java
    ${CMAKE_SWIG_OUTDIR}/SpeechRecognitionEventSignal.java
    ${CMAKE_SWIG_OUTDIR}/SpeechRecognitionCanceledEventSignal.java
    ${CMAKE_SWIG_OUTDIR}/SpeechRecognitionResult.java
    ${CMAKE_SWIG_OUTDIR}/SpeechRecognitionResultPtrFuture.java
    ${CMAKE_SWIG_OUTDIR}/SpeechRecognizer.java
    ${CMAKE_SWIG_OUTDIR}/SpeechRecognizerBase.java
    ${CMAKE_SWIG_OUTDIR}/SpeechTranslationConfig.java

    ${CMAKE_SWIG_OUTDIR}/TranslationRecognizer.java
    ${CMAKE_SWIG_OUTDIR}/TranslationRecognizerBase.java
    ${CMAKE_SWIG_OUTDIR}/TranslationSynthesisEventListener.java
    ${CMAKE_SWIG_OUTDIR}/TranslationSynthesisEventSignal.java
    ${CMAKE_SWIG_OUTDIR}/TranslationSynthesisResult.java
    ${CMAKE_SWIG_OUTDIR}/TranslationSynthesisEventArgs.java
    ${CMAKE_SWIG_OUTDIR}/TranslationTexEventListener.java
    ${CMAKE_SWIG_OUTDIR}/TranslationTexCanceledEventListener.java
    ${CMAKE_SWIG_OUTDIR}/TranslationTextEventSignal.java
    ${CMAKE_SWIG_OUTDIR}/TranslationTextCanceledEventSignal.java
    ${CMAKE_SWIG_OUTDIR}/TranslationRecognitionResult.java
    ${CMAKE_SWIG_OUTDIR}/TranslationRecognitionEventArgs.java
    ${CMAKE_SWIG_OUTDIR}/TranslationRecognitionCanceledEventArgs.java
    ${CMAKE_SWIG_OUTDIR}/TranslationRecognitionResultPtrFuture.java
    ${CMAKE_SWIG_OUTDIR}/StdMapStringString.java
    ${CMAKE_SWIG_OUTDIR}/StdMapStringStringMapIterator.java

    ${CMAKE_SWIG_OUTDIR}/AudioDataStream.java
    ${CMAKE_SWIG_OUTDIR}/SpeechSynthesizer.java
    ${CMAKE_SWIG_OUTDIR}/SpeechSynthesisResult.java
    ${CMAKE_SWIG_OUTDIR}/SpeechSynthesisResultPtrFuture.java
    ${CMAKE_SWIG_OUTDIR}/SpeechSynthesisEventArgs.java
    ${CMAKE_SWIG_OUTDIR}/SpeechSynthesisEventSignal.java
    ${CMAKE_SWIG_OUTDIR}/SpeechSynthesisEventListener.java
    ${CMAKE_SWIG_OUTDIR}/StreamStatus.java
    ${CMAKE_SWIG_OUTDIR}/SpeechSynthesisCancellationDetails.java
    ${CMAKE_SWIG_OUTDIR}/SpeechSynthesisOutputFormat.java

    ${CMAKE_SWIG_OUTDIR}/SWIGTYPE_p_SPXEVENTHANDLE.java
    ${CMAKE_SWIG_OUTDIR}/SWIGTYPE_p_SPXKEYWORDHANDLE.java
    ${CMAKE_SWIG_OUTDIR}/SWIGTYPE_p_SPXLUMODELHANDLE.java
    ${CMAKE_SWIG_OUTDIR}/SWIGTYPE_p_SPXRECOHANDLE.java
    ${CMAKE_SWIG_OUTDIR}/SWIGTYPE_p_SPXRESULTHANDLE.java
    ${CMAKE_SWIG_OUTDIR}/SWIGTYPE_p_SPXSESSIONHANDLE.java
    ${CMAKE_SWIG_OUTDIR}/SWIGTYPE_p_SPXTRIGGERHANDLE.java
    ${CMAKE_SWIG_OUTDIR}/SWIGTYPE_p_SPXCONNECTIONHANDLE.java
    ${CMAKE_SWIG_OUTDIR}/SWIGTYPE_p_SPXGRAMMARHANDLE.java

    ${CMAKE_SWIG_OUTDIR}/VoidFuture.java
    ${CMAKE_SWIG_OUTDIR}/StringVector.java
    ${CMAKE_SWIG_OUTDIR}/UInt8Vector.java
)

set_source_files_properties(${SWIG_JAVA_SOURCES} PROPERTIES GENERATED TRUE)

#
# Set up target to build the JAR, ${JAVA_LIB}
#

set(JAVA_LIB ${PROJECT_NAME})

string(REPLACE . / PACKAGE_DIR ${PROJECT_NAME})

set(JAVA_SOURCES
    ${PACKAGE_DIR}/CancellationDetails.java
    ${PACKAGE_DIR}/KeywordRecognitionModel.java
    ${PACKAGE_DIR}/PropertyCollection.java
    ${PACKAGE_DIR}/RecognitionEventArgs.java
    ${PACKAGE_DIR}/ResultReason.java
    ${PACKAGE_DIR}/CancellationReason.java
    ${PACKAGE_DIR}/CancellationErrorCode.java
    ${PACKAGE_DIR}/NoMatchDetails.java
    ${PACKAGE_DIR}/NoMatchReason.java
    ${PACKAGE_DIR}/Connection.java
    ${PACKAGE_DIR}/PhraseListGrammar.java
    ${PACKAGE_DIR}/Recognizer.java
    ${PACKAGE_DIR}/SessionEventArgs.java
    ${PACKAGE_DIR}/ConnectionEventArgs.java
    ${PACKAGE_DIR}/SpeechConfig.java
    ${PACKAGE_DIR}/SpeechRecognitionResult.java
    ${PACKAGE_DIR}/SpeechRecognitionEventArgs.java
    ${PACKAGE_DIR}/SpeechRecognitionCanceledEventArgs.java
    ${PACKAGE_DIR}/SpeechRecognizer.java

    ${PACKAGE_DIR}/OutputFormat.java
    ${PACKAGE_DIR}/audio/AudioStreamContainerFormat.java

    ${PACKAGE_DIR}/audio/AudioConfig.java
    ${PACKAGE_DIR}/audio/AudioInputStream.java
    ${PACKAGE_DIR}/audio/AudioStreamFormat.java
    ${PACKAGE_DIR}/audio/PullAudioInputStream.java
    ${PACKAGE_DIR}/audio/PushAudioInputStream.java
    ${PACKAGE_DIR}/audio/PullAudioInputStreamCallback

    ${PACKAGE_DIR}/intent/IntentRecognitionResult.java
    ${PACKAGE_DIR}/intent/IntentRecognitionEventArgs.java
    ${PACKAGE_DIR}/intent/IntentRecognitionCanceledEventArgs.java
    ${PACKAGE_DIR}/intent/IntentRecognizer.java
    ${PACKAGE_DIR}/intent/LanguageUnderstandingModel.java

    ${PACKAGE_DIR}/translation/TranslationRecognizer.java
    ${PACKAGE_DIR}/translation/TranslationSynthesisResult.java
    ${PACKAGE_DIR}/translation/TranslationSynthesisEventArgs.java
    ${PACKAGE_DIR}/translation/TranslationRecognitionResult.java
    ${PACKAGE_DIR}/translation/TranslationRecognitionEventArgs.java
    ${PACKAGE_DIR}/translation/TranslationRecognitionCanceledEventArgs.java
    ${PACKAGE_DIR}/translation/SpeechTranslationConfig.java

    ${PACKAGE_DIR}/util/EventHandler.java
    ${PACKAGE_DIR}/util/EventHandlerImpl.java
    ${PACKAGE_DIR}/util/Contracts.java
)
if(NOT ANDROID)
  list(APPEND JAVA_SOURCES ${PACKAGE_DIR}/NativeLibraryLoader.java)
endif()

enable_language(Java)
set(CMAKE_JAVA_COMPILE_FLAGS -source 1.7 -target 1.7)
add_jar(${JAVA_LIB}
        SOURCES ${SWIG_JAVA_SOURCES} ${JAVA_SOURCES}
        OUTPUT_DIR ${CMAKE_LIBRARY_OUTPUT_DIRECTORY})

add_dependencies(${JAVA_LIB} ${JAVA_BINDINGS} ${SWIG_MODULE_${JAVA_BINDINGS}_REAL_NAME})

set_target_properties(${JAVA_LIB} PROPERTIES FOLDER bindings/java)

# Adding logcat logging
if(ANDROID)
    target_link_libraries(${JAVA_BINDINGS} log)
endif()

# Note: this could also run earlier than post-build
add_custom_command(TARGET ${JAVA_LIB}
                   POST_BUILD
                   COMMAND ${CMAKE_COMMAND} -E tar cvf "${CMAKE_LIBRARY_OUTPUT_DIRECTORY}/${JAVA_LIB}-src.zip" --format=zip com
                   WORKING_DIRECTORY "${PROJECT_SOURCE_DIR}"
                   COMMENT "Generating src package including javadoc information")
